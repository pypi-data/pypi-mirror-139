# get only isolate-level information

PREFIX onto: <http://www.ontotext.com/>
PREFIX f: <https://flu-crew.org/term/>

SELECT DISTINCT
    ?isolate
    (GROUP_CONCAT(DISTINCT ?strain;  separator="+") as ?strains)
    (GROUP_CONCAT(DISTINCT ?host;  separator="+") as ?hosts)
    (GROUP_CONCAT(DISTINCT ?subtype;  separator="+") as ?subtypes)
    (GROUP_CONCAT(DISTINCT ?naclade;  separator="+") as ?naclades)
    (GROUP_CONCAT(DISTINCT ?haclade;  separator="+") as ?haclades)
    (GROUP_CONCAT(DISTINCT ?gl_clade;  separator="+") as ?gl_clades)
    (GROUP_CONCAT(DISTINCT ?constellation;  separator="+") as ?constellations)
    (GROUP_CONCAT(DISTINCT ?country;  separator="+") as ?countries)
    (MIN (?date) as ?earliest_date)
FROM onto:disable-sameAs
WHERE {
    ?tag_id f:query_tag ?tag .

    {
        ?sid f:strain_name ?tag .
    } UNION {
        ?lid f:isolate_id ?tag .
    } UNION {
        ?sid f:barcode ?tag .
    } UNION {
        ?lid f:has_segment ?gid .
        ?gid f:seqid ?tag .
    }

    ?lid f:isolate_id ?isolate .
    ?lid f:strain_name ?strain . 
    ?lid f:date ?date .
    OPTIONAL { ?lid f:host ?host . }

    ?sid f:strain_name ?strain .

    OPTIONAL { ?sid f:barcode ?barcode . }
    OPTIONAL { ?sid f:subtype ?subtype . }
    OPTIONAL { ?sid f:constellation ?const . }

    OPTIONAL {
        ?lid f:has_segment ?gid .
        ?gid f:seqid ?seqid .
    }

    OPTIONAL {
        ?lid f:has_segment ?hagid .
        ?hagid f:segment_name "HA" .
        ?hagid f:gl_clade ?gl_clade .
        OPTIONAL { ?hagid f:clade ?haclade }
    }

    OPTIONAL {
        ?lid f:has_segment ?nagid .
        ?nagid f:segment_name "NA" .
        ?nagid f:clade ?naclade .
    }
}
GROUP BY ?isolate
