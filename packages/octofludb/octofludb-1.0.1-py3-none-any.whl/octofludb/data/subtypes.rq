PREFIX f: <https://flu-crew.org/term/>
PREFIX onto: <http://www.ontotext.com/>

SELECT DISTINCT
    ?strain_name
    (GROUP_CONCAT(DISTINCT ?isolate; separator="+") as ?isolates)
    ?segment_name
    (GROUP_CONCAT(DISTINCT ?segment_subtype; separator="+") as ?segment_subtypes)
    (GROUP_CONCAT(DISTINCT ?gisaid_subtype; separator="+") as ?gisaid_subtypes)
    (GROUP_CONCAT(DISTINCT ?genbank_subtype; separator="+") as ?genbank_subtypes)
FROM onto:disable-sameAs
WHERE {
    ?sid f:strain_name ?strain_name .
    ?sid f:has_segment ?gid .
    OPTIONAL { ?sid f:isolate_id ?isolate . }
    ?gid f:segment_name ?segment_name .
    FILTER (?segment_name = "HA" || ?segment_name = "NA") .
    FILTER NOT EXISTS {
        ?sid f:subtype ?subtype .
        FILTER REGEX(?subtype, "H.N.") .
    }
    OPTIONAL { ?gid f:segment_subtype ?segment_subtype . }
    OPTIONAL { ?gid f:has_feature/f:serotype ?genbank_subtype . }
    OPTIONAL { ?sid f:gisaid_subtype ?gisaid_subtype . }
}
GROUP BY ?strain_name ?segment_name
